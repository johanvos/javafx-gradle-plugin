plugins {
    id 'java-gradle-plugin'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'maven-publish'
}

group 'org.openjfx'
version '0.0.8-gvm11-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    compile 'com.gluonhq:omega:0.0.1-gvm11-SNAPSHOT'
    implementation gradleApi()

    implementation 'com.google.gradle:osdetector-gradle-plugin:1.6.1'
    implementation 'org.javamodularity:moduleplugin:1.4.0'

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        javafxPlugin {
            id = 'org.openjfx.javafxplugin'
            displayName = 'JavaFX Gradle Plugin'
            description = 'Plugin that makes it easy to work with JavaFX'
            implementationClass = 'org.openjfx.gradle.JavaFXPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/openjfx/javafx-gradle-plugin'
    vcsUrl = 'https://github.com/openjfx/javafx-gradle-plugin'
    tags = [ 'java', 'javafx' ]
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'javafx-gradle-plugin'
            version = project.version
        }
    }
    repositories {
        maven {
           def repositoryUrl = project.hasProperty('repositoryUrl') ? project.getProperty('repositoryUrl') : 'http://nexus.gluonhq.com/nexus/content/repositories/snapshots/'
           def repositoryUsername = project.hasProperty('repositoryUsername') ? project.getProperty('repositoryUsername') :  project.getProperty('gluonNexusUsername')
           def repositoryPassword = project.hasProperty('repositoryPassword') ? project.getProperty('repositoryPassword') :  project.getProperty('gluonNexusPassword')

           url repositoryUrl
           credentials {
              username repositoryUsername
              password repositoryPassword
          }
      }
   }
}

license {
    skipExistingHeaders = true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
